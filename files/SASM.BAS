1  REM  COPYRIGHT 2013 ADAM GREEN (HTTP://GITHUB.COM/ADAMGREEN)
2  REM  LICENSED UNDER THE APACHE LICENSE, VERSION 2.0
3  REM  SEE APACHE.BAS FOR LICENSE INFO.
4  REM 
5  REM  SIMPLE 6502 SYMBOLIC ASSEMBLER
1000  DATA  "ADC",105,109,101,255,97,113,117,255,125,121,255,255,255,114
1099  DATA  ""
1200  DATA  "ASC","DB","DS","DW","EQU","LST","SAV","TST"
1299  DATA  ""
2000  CLEAR : GOSUB 2100: GOSUB 2800: PRINT "ASSEMBLY STARTED":P% = 1: GOSUB 2900: GOSUB 9600:P% = 2: GOSUB 2900: GOSUB 9900: GOSUB 9700: GOSUB 10100: GOSUB 9800: END 
2100  SPEED= 255: TEXT : PRINT : PRINT  CHR$(4);"PR#3": HOME : GOSUB 2200: GOSUB 2300: GOSUB 2400: GOSUB 2600: RETURN 
2200 P =  PEEK(116) *256 + PEEK(115): IF P = 30208  THEN  RETURN 
2210  IF P < >38400  THEN  PRINT "*HIMEM": STOP 
2220  IF  PEEK(110) *256 + PEEK(109) > = 30208  THEN  PRINT "*NORAM": STOP 
2230  HIMEM: 30208: RETURN 
2300 LF% = 1:TF% = 0:IC% = 0:DC% = 0:SC% = 0:EC% = 0:TW% = 768:TR% = 768:TC% = 0
2310 D$ =  CHR$(4)
2320  DIM O%(13): DIM SN$(255): DIM SV%(255)
2330  RETURN 
2400  READ T$: IF T$ = ""  THEN 2420
2410 IC% = IC% +1: GOSUB 2500: GOTO 2400
2420  READ T$: IF T$ = ""  THEN  RETURN 
2430 DC% = DC% +1: GOTO 2420
2500  FOR K = 1 TO 14: READ T%: NEXT : RETURN 
2600  GOSUB 2700:S1% = D1%:S2% = D2%:S3% = D3%:S4% = D4%: RETURN 
2700 D1% =  PEEK(123):D2% =  PEEK(124):D3% =  PEEK(125):D4% =  PEEK(126): RETURN 
2800  RETURN 
2900 P = 31232
2910  GOSUB 3000: GOSUB 3700: IF O$ = "END"  THEN  RETURN 
2920  GOTO 2910
3000  GOSUB 3100: GOSUB 3200: READ A$: GOSUB 2700:A% =  LEN(A$):A = P:E = 65536:I = 1: GOSUB 3300: IF R%  THEN  RETURN 
3010 L$ = T$: GOSUB 3300: IF R%  THEN  RETURN 
3020 O$ = T$: GOSUB 3300: IF R%  THEN  RETURN 
3030 P$ = T$: GOSUB 3300: IF   NOT R%  THEN  GOSUB 3600: PRINT "EXTRA_INPUT"
3040  RETURN 
3100 T$ = "":A$ = "":L$ = "":O$ = "":P$ = "":C$ = "":S$ = "":E$ = ""
3110  PRINT D$;"FRE(0)": RETURN 
3200  POKE 123,D1%: POKE 124,D2%: POKE 125,D3%: POKE 126,D4%: RETURN 
3300  IF I = A% +1  THEN R% = 1: RETURN 
3310  IF  MID$ (A$,I,1) = ";"  THEN C$ =  MID$ (A$,I):R% = 1: RETURN 
3320  GOSUB 3400:T$ =  MID$ (A$,I,K -I):I = K: GOSUB 3500:I = K:R% = 0: RETURN 
3400 K = I
3410  IF K >A%  THEN  RETURN 
3420  IF  MID$ (A$,K,1) = " "  THEN  RETURN 
3430 K = K +1: GOTO 3410
3500 K = I
3510  IF K >A%  THEN  RETURN 
3520  IF  MID$ (A$,K,1) < >" "  THEN  RETURN 
3530 K = K +1: GOTO 3510
3600 EC% = EC% +1: PRINT "LINE:";D2% *256 +D1%;" *";: RETURN 
3700  IF TF%  AND P% = 2  THEN  GOSUB 3800
3710  GOSUB 4300:F% = 0: GOSUB 4600: GOSUB 7700: IF   NOT F%  AND O$ < >"END"  THEN  GOSUB 3600: PRINT "INVALID_OPCODE"
3720  IF LF%  AND P% = 2  THEN  GOSUB 8800
3730  RETURN 
3800  GOSUB 3900: IF   NOT R%  THEN  RETURN 
3810  GOSUB 4000: RETURN 
3900  IF C$ = ""  THEN  RETURN 
3910  FOR I = 2 TO  LEN(C$):T$ =  MID$ (C$,I,1): IF T$ < >" "  AND (T$ <"0"  OR T$ >"9")  AND (T$ <"A"  OR T$ >"F")  THEN R% = 0: RETURN 
3920  NEXT :R% = 1: RETURN 
4000 I = 2
4010  IF I > =  LEN(C$)  THEN  RETURN 
4020  GOSUB 4100:E$ =  MID$ (C$,I,2):M = 1: GOSUB 6000: GOSUB 4200:I = I +2: GOTO 4010
4100  IF I > =  LEN(C$)  THEN  RETURN 
4110  IF  MID$ (C$,I,1) < >" "  THEN  RETURN 
4120 I = I +1: GOTO 4100
4200  IF TC% = 256  THEN  GOSUB 3600: PRINT "TST_OVER": STOP 
4210  POKE TW%,E%:TW% = TW% +1: IF TW% >1023  THEN TW% = 768
4220 TC% = TC% +1: RETURN 
4300  IF L$ = ""  OR P% = 2  THEN  RETURN 
4310 LI% =  -1:S$ = L$: GOSUB 4400: IF SI% < > -1  THEN  GOSUB 3600: PRINT "DUP_LABEL": RETURN 
4320  GOSUB 4500: IF SI% =  -1  THEN  GOSUB 3600: PRINT "TOO_MANY_LABELS": STOP 
4330 LI% = SI%:SN$(LI%) = L$:SV%(LI%) = P: RETURN 
4400  IF SC% = 0  THEN SI% =  -1: RETURN 
4410  FOR M = 0 TO SC% -1: IF SN$(M) = S$  THEN SI% = M: RETURN 
4420  NEXT :SI% =  -1: RETURN 
4500  IF SC% = 256  THEN SI% =  -1: RETURN 
4510 SI% = SC%:SC% = SC% +1: RETURN 
4600  IF O$ = ""  THEN F% = 1: RETURN 
4610  GOSUB 4700: IF   NOT F%  THEN  RETURN 
4620  GOSUB 4900: RETURN 
4700  RESTORE : FOR I = 1 TO IC%: READ T$: IF T$ = O$  THEN F% = 1: GOSUB 4800: RETURN 
4710  GOSUB 2500
4720  NEXT : READ T$: RETURN 
4800  FOR K = 0 TO 13: READ O%(K): NEXT : RETURN 
4900 L% =  LEN(P$): GOSUB 5000: IF P$ = ""  THEN  GOSUB 5100: RETURN 
4910  IF  MID$ (P$,1,1) = "#"  THEN  GOSUB 5700: RETURN 
4920  GOSUB 6300: IF R%  THEN  GOSUB 6400: RETURN 
4930  GOSUB 6800: IF R%  THEN  GOSUB 6900: RETURN 
4940  GOSUB 7000: IF R%  THEN  GOSUB 7100: RETURN 
4950  GOSUB 7200: IF R%  THEN  GOSUB 7300: RETURN 
4960  GOSUB 7400: IF R%  THEN  GOSUB 7500: RETURN 
4970  GOSUB 7600: RETURN 
5000 OP% =  -1:CP% =  -1:CM% =  -1: IF L% = 0  THEN  RETURN 
5010  FOR I = 1 TO L%:T$ =  MID$ (P$,I,1): IF T$ = "("  THEN OP% = I
5020  IF T$ = ")"  THEN CP% = I
5030  IF T$ = ","  THEN CM% = I
5040  NEXT : RETURN 
5100  IF O%(3) = 255  THEN  GOSUB 5200: RETURN 
5110 X% = O%(3): GOSUB 5300: RETURN 
5200  GOSUB 3600: PRINT "BAD_ADDR": RETURN 
5300  IF P% = 2  AND X% < > PEEK(P)  THEN  GOSUB 3600: PRINT "PHASE": STOP 
5310  GOSUB 5400: RETURN 
5400  IF P > = 38400  THEN  GOSUB 3600: PRINT "OOM": STOP 
5410  POKE P,X%:P = P +1: IF TF%  AND P% = 2  THEN  GOSUB 5500
5420  RETURN 
5500  GOSUB 5600: IF T% < >X%  THEN  GOSUB 10000
5510  RETURN 
5600  IF TC% = 0  THEN  GOSUB 3600: PRINT "TST_UNDER": STOP 
5610 T% =  PEEK(TR%):TR% = TR% +1: IF TR% >1023  THEN TR% = 768
5620 TC% = TC% -1: RETURN 
5700  IF 2 >L%  THEN  GOSUB 5800: RETURN 
5710 T$ =  MID$ (P$,2,1): IF T$ = "<"  THEN E$ =  MID$ (P$,3): GOSUB 5900:E% = E% - INT(E%/256) *256: GOTO 5740
5720  IF T$ = ">"  THEN E$ =  MID$ (P$,3): GOSUB 5900:E% = (E%/256): GOTO 5740
5730 E$ =  MID$ (P$,2): GOSUB 5900
5740  IF O%(0) = 255  THEN  GOSUB 5200: RETURN 
5750 X% = O%(0): GOSUB 5300: IF E% >255  THEN  GOSUB 3600: PRINT "TOO_BIG": RETURN 
5760 X% = E%: GOSUB 5400: RETURN 
5800  GOSUB 3600: PRINT "BAD_EXPR": RETURN 
5900  IF E$ = ""  THEN  GOSUB 5800: RETURN 
5910 T$ =  MID$ (E$,1,1): IF T$ = "$"  THEN M = 2: GOSUB 6000: RETURN 
5920  IF T$ > = "0"  AND T$ < = "9"  THEN  GOSUB 6100: RETURN 
5930  GOSUB 6200: RETURN 
6000 N% =  LEN(E$): IF N% -M +1 <1  THEN  GOSUB 5800: RETURN 
6010 E% = 0
6015  IF M >N%  THEN  RETURN 
6017 T% =  ASC( MID$ (E$,M,1)): IF T% > = 48  AND T% < = 57  THEN T% = T% -48: GOTO 6040
6020  IF T% > = 65  AND T% < = 70  THEN T% = T% -55: GOTO 6040
6030 E% =  -1: GOSUB 3600: PRINT "BAD_HEX": RETURN 
6040 E% = E% *16 +T%:M = M +1: GOTO 6015
6100 E% =  VAL(E$): RETURN 
6200 S$ = E$: GOSUB 4400: IF SI% < > -1  THEN E% = SV%(SI%): RETURN 
6210  IF P% = 1  THEN E% = 256: RETURN 
6220 E% = 256: GOSUB 3600: PRINT "BAD_LABEL": RETURN 
6300 R% = CM% =  -1  AND OP% =  -1  AND CP% =  -1: RETURN 
6400 E$ = P$: GOSUB 5900: IF O%(10) = 255  THEN I = 2:K = 1: GOSUB 6500: RETURN 
6410 E% = E% -P +2: IF E% < -128  OR E% >127  THEN  GOSUB 3600: PRINT "TOO_FAR": RETURN 
6420 X% = O%(10): GOSUB 5300: GOSUB 6600: RETURN 
6500  IF E% <256  AND O%(I) < >255  THEN X% = O%(I): GOSUB 5300: GOSUB 6600: RETURN 
6510  IF O%(K) < >255  THEN X% = O%(K): GOSUB 5300: GOSUB 6700: RETURN 
6520  GOSUB 5200: RETURN 
6600 X% = E% - INT(E%/256) *256: GOSUB 5400: RETURN 
6700  GOSUB 6600:X% = E%/256: GOSUB 5400: RETURN 
6800 R% = CM% < > -1  AND OP% =  -1  AND CP% =  -1: RETURN 
6900 E$ =  MID$ (P$,1,CM% -1): GOSUB 5900:T$ =  MID$ (P$,CM% +1,1): IF T$ = "X"  THEN I = 6:K = 8: GOSUB 6500: RETURN 
6910  IF T$ = "Y"  THEN I = 7:K = 9: GOSUB 6500: RETURN 
6920  GOSUB 5200: RETURN 
7000 R% = CM% < > -1  AND OP% < > -1  AND CP% < > -1  AND CP% >CM%  AND OP% <CP%: RETURN 
7100 E$ =  MID$ (P$,OP% +1,CM% -OP% -1): GOSUB 5900: IF  MID$ (P$,CM% +1,1) < >"X"  THEN  GOSUB 5200: RETURN 
7110 I = 4:K = 12: GOSUB 6500: RETURN 
7200 R% = CM% < > -1  AND OP% < > -1  AND CP% < > -1  AND CP% <CM%  AND OP% <CP%: RETURN 
7300 E$ =  MID$ (P$,OP% +1,CP% -OP% -1): GOSUB 5900: IF  MID$ (P$,CM% +1,1) < >"Y"  THEN  GOSUB 5200: RETURN 
7310  IF O%(5) < >255  THEN X% = O%(5): GOSUB 5300: GOSUB 6600: RETURN 
7320  GOSUB 5200: RETURN 
7400 R% = CM% =  -1  AND OP% < > -1  AND CP$ < > -1  AND OP% <CP%: RETURN 
7500 E$ =  MID$ (P$,OP% +1,CP% -OP% -1): GOSUB 5900:I = 13:K = 11: GOSUB 6500: RETURN 
7600  GOSUB 3600: PRINT "BAD_OPERAND": RETURN 
7700  IF F%  THEN  RETURN 
7710  GOSUB 7800: IF   NOT F%  THEN  RETURN 
7720 L% =  LEN(P$): ON I GOSUB 7900,8000,8200,8300,8400,8500,8600,8700: RETURN 
7800  FOR I = 1 TO DC%: READ T$: IF T$ = O$  THEN F% = 1: RETURN 
7810  NEXT : RETURN 
7900  IF  MID$ (P$,1,1) < >"'"  THEN  GOSUB 7600: RETURN 
7910 L% = L% -1: FOR I = 2 TO L%:X% =  ASC( MID$ (P$,I,1)): GOSUB 5300: NEXT : IF  MID$ (P$,L% +1,1) < >"'"  THEN  GOSUB 7600
7920  RETURN 
8000  FOR I = 1 TO L%: GOSUB 8100:E$ =  MID$ (P$,I,K -I): GOSUB 5900: GOSUB 6600:I = K: NEXT : RETURN 
8100  FOR K = I TO L%: IF  MID$ (P$,K,1) = ","  THEN  RETURN 
8110  NEXT : RETURN 
8200  IF P$ = "/"  THEN E% =  INT((P +256)/256) *256 -P: GOTO 8220
8210 E$ = P$: GOSUB 5900
8220 X% = 0: FOR I = 1 TO E%: GOSUB 5300: NEXT : RETURN 
8300  FOR I = 1 TO L%: GOSUB 8100:E$ =  MID$ (P$,I,K -I): GOSUB 5900: GOSUB 6700:I = K: NEXT : RETURN 
8400 E$ = P$: GOSUB 5900: IF LI% =  -1  THEN  GOSUB 3600: PRINT "EQU_NOLABEL": RETURN 
8410 SV%(LI%) = E%:E = E%: RETURN 
8500  IF P% = 1  THEN  RETURN 
8510  IF P$ = "ON"  THEN LF% = 1: RETURN 
8520  IF P$ = "OFF"  THEN LF% = 0: RETURN 
8530  GOSUB 7600: RETURN 
8600  IF P% = 2  AND EC% = 0  THEN  PRINT D$;"BSAVE ";P$;",A31232,L";P -31232
8610  RETURN 
8700 TF% = 1: RETURN 
8800  GOSUB 8900: GOSUB 9000: IF E < >65536  THEN  GOSUB 9300: PRINT : RETURN 
8810  GOSUB 9400: PRINT : RETURN 
8900 T$ =  STR$(D2% *256 +D1%): PRINT  TAB( 6 - LEN(T$));T$;":";: RETURN 
9000 X% = A/256: GOSUB 9100:X% = A -(X% *256): GOSUB 9100: RETURN 
9100 T% = X%/16: GOSUB 9200:T% = X% -(T% *16): GOSUB 9200: RETURN 
9200  IF T% <10  THEN  PRINT  CHR$(48 +T%);: RETURN 
9210  PRINT  CHR$(55 +T%);: RETURN 
9300  PRINT " =";: GOSUB 10200: GOSUB 9500: RETURN 
9400  IF A = P  THEN 9440
9410  FOR I = 0 TO P -A -1: IF I = 3  THEN  GOSUB 9500
9420  IF I >2  AND  INT(I/3) *3 = I  THEN  PRINT 
9430  HTAB (12 +3 *(I - INT(I/3) *3)):X% =  PEEK(A +I): GOSUB 9100: NEXT 
9440  IF P -A < = 3  THEN  GOSUB 9500
9450  RETURN 
9500  PRINT  TAB( 22);A$;: RETURN 
9600 D1% = S1%:D2% = S2%:D3% = S3%:D4% = S4%: RETURN 
9700  RETURN 
9800  HIMEM: 38400: RETURN 
9900  IF P% = 1  OR   NOT TF%  THEN  RETURN 
9910  IF TC% < >0  THEN  GOSUB 10000
9920  RETURN 
10000  GOSUB 3600: PRINT "TST_FAIL": STOP 
10010  RETURN 
10100  PRINT : PRINT "ASSEMBLY COMPLETED OF ";P -31232;" BYTES": PRINT EC%;" ERROR(S) ENCOUNTERED": PRINT : FOR I = 0 TO SC% -1: PRINT SN$(I);"= ";:E = SV%(I): GOSUB 10200: PRINT " (";SV%(I);")": NEXT : RETURN 
10200 X% = E/256: GOSUB 9100:X% = E -(X% *256): GOSUB 9100: RETURN 
32000  DATA  " TST"
32005  DATA  "LABEL ADC #$20 ;69 20"
32010  DATA  "LONLY"
32015  DATA  ";6520"
32020  DATA  " ADC $20"
32025  DATA  "NOP ;NO OP"
32030  DATA  " ADC $120 ;6D2001"
32035  DATA  ";6114"
32040  DATA  "L1 ADC (20,X)"
32045  DATA  "L1 ;DUPE LABEL"
32050  DATA  " ADC NONE ;6D0001"
32055  DATA  " ADC FWD ;6D0F7A"
32060  DATA  "FWD"
32065  DATA  "TWO EQU 2"
32070  DATA  " ADC (TWO),Y ;7102"
32075  DATA  " DS 1 ;00"
32080  DATA  " DB 1 ;01"
32085  DATA  " DB 254,$FF,TWO ;FEFF02"
32090  DATA  " DW 256 ;0001"
32095  DATA  " DW 0,$3412,TWO ;000012340200"
32100  DATA  " ASC 'A' ;41"
32105  DATA  " ASC 'A09Z' ;4130395A"
32110  DATA  " ADC #>$1234 ;6912"
32115  DATA  " ADC #<$1234 ;6934"
63997  DATA  " SAV TEST.BIN"
63998  DATA  " LST OFF"
63999  DATA  " END"