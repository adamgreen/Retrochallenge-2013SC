14  REM  * CONTROLLER CHECKOUT
18  REM  * TOM RILEY COPYRIGHT 83
24  REM 
25 OP$ = "OPEN":CL$ = "CLOSED":BL$ = "      "
30  TEXT : HOME 
34  REM 
35  REM  ** DISPLAY FORMAT
40  VTAB 2: HTAB 6
50  PRINT "CONTROLLER CHECKOUT - APPLE II"
60  VTAB 6
70  PRINT "   THIS PROGRAM TESTS CONTROLLER"
80  PRINT "   PUSHBUTTONS AND POTS."
90  PRINT 
100  VTAB 10: HTAB 8: PRINT "PB0 IS "
110  VTAB 12: HTAB 8: PRINT "PB1 IS "
120  VTAB 14: HTAB 8: PRINT "PB2 IS "
140  VTAB 17: HTAB 14: PRINT "PDL(0) = "
150  VTAB 18: HTAB 14: PRINT "PDL(1) = "
160  VTAB 19: HTAB 14: PRINT "PDL(2) = "
170  VTAB 20: HTAB 14: PRINT "PDL(3) = "
190  FOR I = 10 TO 14  STEP 2: VTAB I: HTAB 15: PRINT OP$: NEXT 
198  REM 
200  REM  ** READ PUSHBUTTONS
210 P0 =  PEEK( -16287)
220 P1 =  PEEK( -16286)
230 P2 =  PEEK( -16285)
237  VTAB 10: HTAB 25: PRINT BL$;: HTAB 25: PRINT P0
238  VTAB 12: HTAB 25: PRINT BL$;: HTAB 25: PRINT P1
239  VTAB 14: HTAB 25: PRINT BL$;: HTAB 25: PRINT P2
245  REM  ** READ  CONTROLLER POTS
250 G0 =  PDL(0)
252  FOR I = 1 TO 10: NEXT I: REM  DELAY REQUIRED
255 G1 =  PDL(1)
257  FOR I = 1 TO 10: NEXT I
260 G2 =  PDL(2)
262  FOR I = 1 TO 10: NEXT I
265 G3 =  PDL(3)
270  FOR I = 17 TO 20: VTAB I: HTAB 23: PRINT BL$: NEXT I
280  VTAB 17: HTAB 23: PRINT G0: VTAB 18: HTAB 23: PRINT G1: VTAB 19: HTAB 23: PRINT G2: VTAB 20: HTAB 23: PRINT G3
285  REM  * PUSHBUTTON OPEN OR CLOSED
290  IF P0 >127  OR P1 >127  OR P2 >127  THEN  GOTO 300
294  FOR I = 10 TO 14  STEP 2: VTAB I: HTAB 15: PRINT OP$: NEXT 
296  GOTO 210
300  IF P0 >127  THEN  VTAB 10: HTAB 15: PRINT CL$
305  IF P0 <128  THEN  VTAB 10: HTAB 15: PRINT OP$
310  IF P1 >127  THEN  VTAB 12: HTAB 15: PRINT CL$
315  IF P1 <128  THEN  VTAB 12: HTAB 15: PRINT OP$
320  IF P2 >127  THEN  VTAB 14: HTAB 15: PRINT CL$
325  IF P2 <128  THEN  VTAB 14: HTAB 15: PRINT OP$
330  GOTO 210
999  END 